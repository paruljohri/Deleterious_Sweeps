// Purpose:
// run recurrent sweeps model with population split, DFE is not rescaled following population size changes

// Parameters (from Arguello et al 2019 (BNZ model)):
// african Ne: 3.91 x 10^6
// european Ne: 4.73 x 10^5
// ancestral Ne: 1.95 x 10^6
// generations since split: 6.62 x 10^4
// time and size scaled down by 100
// mutation and recombination scaled up by 100

// Run with: slim -d "replicate='x'" arguello_deleterious_norescale.slim

// Simulation parameters: 
initialize()
{
	// variables
	defineConstant("mu", (4.044534*(10^(-7)))); // total mutation rate for f0, f1, f2, f3 classes
	defineConstant("r", (10^(-6)));
	defineConstant("Ne", 19500); // ancestral Ne
	defineConstant("rep", replicate);

	// set the overall mutation rate
	initializeMutationRate(mu);

	// m1 mutation type (effectively neutral and semi-dominant) f0
	initializeMutationType("m1", 0.5, "s", "runif(1, (-1/(2*Ne)), 0);");

	// m2 mutation type (weakly deleterious and semi-dominant) f1
	initializeMutationType("m2", 0.5, "s", "runif(1, (-10/(2*Ne)), (-1/(2*Ne)));");
	
	// m3 mutation type (deleterious and semi-dominant) f2
	initializeMutationType("m3", 0.5, "s", "runif(1, (-100/(2*Ne)), (-10/(2*Ne)));");

	// m4 mutation type (strongly deleterious and semi-dominant) f3
	initializeMutationType("m4", 0.5, "s", "runif(1, (-10000/(2*Ne)), (-100/(2*Ne)));");

	// g1 genomic element type, uses deleterious mutations
	initializeGenomicElementType("g1", c(m1, m2, m3, m4), c(0.247, 0.494, 0.039, 0.219));

	// uniform chromosome of lenth 10 kb with deleterious mutations occuring in 500 bp center
	initializeGenomicElement(g1, 0, 10000);

	// uniform recombination along the chromosome
	initializeRecombinationRate(r);
}

// First generation
1 late()
{
	// create a population of 19500 individuals
	sim.addSubpop("p1", Ne);
}

// Run simulation for a burn in period of 10Ne generations
195000 early()
{
	// split off two subpopulations (African and European) from source population
	sim.addSubpopSplit("p2", 39100, p1); // African Ne
	sim.addSubpopSplit("p3", 4730, p1); // European Ne
}

// Reduce the ancestral population size to 0 after subpopulations have been populated
195000 late()
{
	// remove the source subpopulation
	p1.setSubpopulationSize(0); 
}

// Run simulation for an additional 662 generations to model split time
195662 late()
{
	// sampling the genomes
	afr = sample(p2.genomes, 100, F);
	eur = sample(p3.genomes, 100, F);

	// vector of all sampled genomes
	genomes = c(afr, eur);

	// list of segregating sites sorted by position
	full_muts = sortBy(unique(genomes.mutations), "position");

	// create an MS file to write to
	ms_full_muts = paste0(c("folder/file_prefix", rep, "_full_muts.ms"));
	writeFile(ms_full_muts, "", append = F);

	// formatting the header
	writeFile(ms_full_muts, paste0(c("\\\\", "\n", "segsites: ", size(full_muts))), append = T);
	
	// create a list of positions of the seg sites
	positions_full_muts = format("%.6f", full_muts.position / sim.chromosome.lastPosition);
	writeFile(ms_full_muts, paste0(c("positions: ", paste(positions_full_muts, " "))), append = T);

	// printing the sample genotypes
	for (genome in afr)
	{
		hasMuts = (match(full_muts, genome.mutations) >= 0);
		writeFile(ms_full_muts, paste0(paste(asInteger(hasMuts), "")), append = T);
	}
	for (genome in eur)
	{
		hasMuts = (match(full_muts, genome.mutations) >= 0);
		writeFile(ms_full_muts, paste0(paste(asInteger(hasMuts), "")), append = T);
	}

	// output full information from simulation
	full = paste0(c("folder/file_prefix", rep, "_full_muts.full"));
	genomes.output(full, append=F);
	sim.simulationFinished();
}
